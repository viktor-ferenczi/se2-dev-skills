@echo off

echo Verifying Python
python --version
if %ERRORLEVEL% EQU 0 goto has_python
echo ERROR: Missing Python
echo Please install Python 3.13 or newer.
echo Make sure python.exe is on PATH.
goto failed
:has_python

if exist Data goto skip_data_link
if not exist "%TEMP%\se2-dev-plugin" mkdir "%TEMP%\se2-dev-plugin"
echo Creating Data junction to %TEMP%\se2-dev-plugin
mklink /J Data "%TEMP%\se2-dev-plugin" >NUL
if %ERRORLEVEL% NEQ 0 goto failed
:skip_data_link

uv -V 2>NUL
if %ERRORLEVEL% EQU 0 goto skip_uv
echo Installing uv
powershell -ExecutionPolicy ByPass -c "irm https://astral.sh/uv/install.ps1 | iex"
uv -V
if %ERRORLEVEL% NEQ 0 goto failed
:skip_uv

if exist .venv goto skip_venv
echo Setting up Python .venv (uv sync)
uv sync
:skip_venv

if exist busybox.exe goto skip_busybox
echo Downloading busybox
powershell -Command "$ProgressPreference = 'SilentlyContinue'; Invoke-WebRequest -Uri https://frippery.org/files/busybox/busybox64u.exe -OutFile busybox.exe"
if %ERRORLEVEL% NEQ 0 goto failed
:skip_busybox

echo Downloading PluginHub-SE2 registry
uv run download_pluginhub.py
if %ERRORLEVEL% NEQ 0 goto failed

echo Indexing plugin code (skipped if no sources downloaded yet)
uv run index_plugin_code.py
if %ERRORLEVEL% NEQ 0 goto failed

echo DONE
echo DONE >Prepare.DONE
exit /b 0

:failed
echo FAILED
exit /b 1
